
func nb-arrow(nodeset arrow) {
    default = {
        'search': 'true'
        'buttonContent': 'Find'
    }

    nb-block('arrow', nb-extend(default, arrow))
}

match .arrow nb {
    <form class="nb-arrow nb-search-arrow">

        apply . nb-main-attrs

        <span class="nb-arrow-tail"></span>

        input-label = if .text {
            '<a href="' + .href + '" class="nb-search-arrow-label">' + .text + '</a>'
        } else {
            ''
        }

        input-requests = if .requests {
            ' — ' + .requests
        } else {
            ''
        }

        nb-button({
            'size': 'm'
            'class': 'nb-search-arrow-button'
            'content': .buttonContent
            'attrs': {
                'type': 'submit'
            }
        })

        <div class="nb-search-arrow-content">
            nb-input({
                'size': 'm'
                'class': 'nb-search-arrow-input'
                'leftContent': input-label
                'attrs': {
                    'autocomplete': 'off'
                    'value': .value
                    'name': 'text'
                }
                'hint': {
                    'text': input-requests
                    'ghost': true()
                }
            })
        </div>

    </form>
}

match .arrow[.search != 'true'] nb {
    <div class="nb-service-arrow">

        apply . nb-main-attrs

        <span class="nb-arrow-tail"></span>

        .text
    </div>
}

match .arrow[.search != 'true'][.href] nb {
    <a href="{.href}" class="nb-service-arrow">

        apply . nb-main-attrs

        <span class="nb-arrow-tail"></span>

        .text
    </a>
}
