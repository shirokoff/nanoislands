func nb-input(nodeset options) {
    default = {
        'size': 's'
    }

    if options.left || options.right || options.label || options.reset || options.ghost {
        nb-block('input', nb-extend(default, options))
    } else if options.type == 'multiline' {
        nb-block('textarea', nb-extend(default, options))
    } else {
        nb-block('inputSimple', nb-extend(default, options))
    }
}

match .input nb {
    <label class='nb-input nb-input_type_complex'>
        apply . nb-main-attrs

        if !( .static ){
            @data-nb='input'
        }
        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-disabled'
            @data-nb-disabled = 'true'
        }

        if .ghost {
            @class += ' is-ghost'
            @data-nb-ghost = 'true'
        }

        if .reset {
            <span class="nb-input__reset"></span>
        }

        <span class="nb-input__content">
            <input class="nb-input__controller" value="{.content}">
                for .attrs.* {
                    @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
            <span class="nb-input__view">&nbsp;</span>
        </span>
    </label>
}

match .inputSimple nb {
    <input value="{.content}" data-nb-type='simple' class='nb-input _init nb-input_type_simple'>
        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }

        if !( .static ){
            @data-nb='input'
        }
        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }
    </input>
}

match .textarea nb {
    <textarea data-nb-type='simple' class='nb-input _init nb-input_type_simple'>
        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }

        if !( .static ){
            @data-nb='input'
        }
        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }

        if .content {
            html(.content)
        }

    </textarea>
}
