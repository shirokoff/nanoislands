func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        nb-block('buttonAttach', nb-extend(default, options))
    } else if options.href {
        nb-block('buttonLink', nb-extend(default, options))
    } else {
        nb-block('button', nb-extend(default, options))
    }
}

match .button nb {
    <button class="nb-button _init nb-button_size_{ .size }  nb-button_theme_{ .theme }">
        if !( .static ) {
            @data-nb = 'button'
        }

        apply . nb-main-attrs

        if .disabled {
            @class += ' is-disabled'
            @disabled = 'disabled'
        }

        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-button__text">
           apply .icon nb
               if .content {
                   html(.content)
               }
        </span>
    </button>
}

match .buttonLink nb {
    <a class="nb-button _init nb-button_size_{ .size }  nb-button_theme_{ .theme }">
        apply . nb-main-attrs

        if .disabled {
            @class += ' nb-button_disabled'
            @disabled = 'disabled'
        }

        if !( .disabled ) && .href {
            @href="{ .href }"
        }

        if !( .static ) {
            @data-nb = 'button'
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-button__text">
           apply .icon nb
               if .content {
                   html(.content)
               }
        </span>
    </a>
}

match .buttonAttach nb {
    <label class="nb-button _init nb-button_size_{ .size } nb-button_type_attach  nb-button_theme_{ .theme }">
        apply . nb-main-attrs

        if .disabled {
            @class += ' nb-button_disabled'
            @disabled = 'disabled'
        }

        if !( .disabled ) && .href {
            @href="{ .href }"
        }

        if !( .static ) {
            @data-nb = 'button'
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-file-intruder">
            <span class="nb-file-intruder__inner">
                <input class="nb-file-intruder__input" type="file">
                    if .attrs.multiple || .multiple {
                        @multiple = "multiple"
                    }

                    if .name {
                        @name = .name
                    }

                    if .attrs.name {
                        @name = "{.attrs.name}"
                    }
                    if .attrs.title {
                        @title += "{.attrs.title}"
                    }
                </input>
                <span class="nb-file-intruder__focus"></span>
            </span>
        </span>
        <span class="nb-button__text">
            apply .icon nb
            if (.content) {
                html(.content)
            }
        </span>
    </label>
}
